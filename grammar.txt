program        → declaration* EOF;

declaration    → varDecl
               | funDecl
               | statement ;

varDecl        → "var" IDENTIFIER ( "=" expression )? ";" ;
funDecl        → "proc" IDENTIFIER "(" funParams? ")" block ;

funParams      → IDENTIFIER ("," IDENTIFIER)* ;

statement      → exprStmt
               | printStmt
               | block
               | ifStmt
               | whileStmt
               | forStmt
               | returnStmt ;

exprStmt       → expression ";" ;
printStmt      → "print" expression ";" ;
block          → "{" declaration* "}" ;
ifStmt         → "if" "(" expression ")" statement
                 ( "else" statement )? ;
whileStmt      → "while" "(" expression ")" statement ;
forStmt        → "for" "("
                    ( varDecl | exprStmt | ";" )
                    expression? ";"
                    expression?
                 ")" statement
               ;
returnStmt     → "return" expression? ";" ;

expression     → compound ;
compound       → assignment ( (",") assignment )* ;
assignment     → IDENTIFIER "<-" assignment
               | logical_or ;

logical_or     → logical_and ( "||" logical_and )* ;
logical_and    → equality ( "&&" equality )* ;

equality       → comparison ( ( "!=" | "==" ) comparison )* ;
comparison     → term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           → factor ( ( "-" | "+" ) factor )* ;
factor         → unary ( ( "/" | "*" ) unary )* ;
unary          → ( "!" | "-" ) unary
               | call ;
call           → primary "(" funArgs? ")" ;
primary        → NUMBER | STRING | "true" | "false" | "nil"
               | "(" expression ")"
               | IDENTIFIER ;

funArgs        → expression ("," expression)* ;
